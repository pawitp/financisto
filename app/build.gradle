plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// Load properties from keys.properties file
def keysPropertiesFile = rootProject.file('keys.properties')
def keysProperties = new Properties()
if (keysPropertiesFile.exists()) {
    keysProperties.load(new FileInputStream(keysPropertiesFile))
}

android {
    namespace 'ru.orangesoftware.financisto'
    compileSdkVersion 36

    defaultConfig {
        applicationId "ru.orangesoftware.financisto"
        minSdkVersion 33
        targetSdkVersion 34
        versionCode 122
        versionName "1.8.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        buildConfigField "String", "DROPBOX_APP_KEY", "\"${keysProperties['DROPBOX_APP_KEY'] ?: ''}\""
        manifestPlaceholders = [dropboxAppKey: keysProperties['DROPBOX_APP_KEY'] ?: '']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    sourceSets {
        test {
            resources.srcDirs += ['src/test/resources']
        }
    }
    packagingOptions {
        resources {
            excludes += [
                    'META-INF/DEPENDENCIES.txt',
                    'META-INF/LICENSE',
                    'META-INF/LICENSE.txt',
                    'META-INF/NOTICE.txt',
                    'META-INF/NOTICE',
                    'META-INF/LICENSE',
                    'META-INF/DEPENDENCIES',
                    'META-INF/notice.txt',
                    'META-INF/license.txt',
                    'META-INF/dependencies.txt',
                    'META-INF/LGPL2.1'
            ]
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    buildFeatures {
        buildConfig true
    }
}

def AAVersion = '4.6.0'

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'

    implementation "org.androidannotations:androidannotations-api:$AAVersion"

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'net.sf.trove4j:trove4j:3.0.3'

    implementation 'com.dropbox.core:dropbox-core-sdk:5.2.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.wdullaer:materialdatetimepicker:3.6.4'
    implementation 'commons-io:commons-io:2.5'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'org.reactivestreams:reactive-streams:1.0.2'
    implementation 'com.github.bumptech.glide:glide:4.10.0'

    implementation fileTree(include: '**/*.jar', dir: 'libs')

    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.robolectric:robolectric:4.2.1'
}